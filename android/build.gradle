// Top-level build file where you can add configuration options common to all sub-projects/modules.

def vstsUsername = System.getenv("NPM_USER") != null ?
        System.getenv("NPM_USER") : project.findProperty("vstsUsername")
def vstsMavenAccessTokenEncoded = System.getenv("NPM_PASS") != null ?
        System.getenv("NPM_PASS") : project.findProperty("vstsMavenAccessToken")
def vstsMavenAccessToken = new String(Base64.decoder.decode(vstsMavenAccessTokenEncoded))

buildscript {
    ext {
         // Required by react-native-push-notification
        googlePlayServicesVersion = "17.0.0" // default: "+"
        firebaseMessagingVersion = "21.1.0" // default: "21.1.0"

        buildToolsVersion = "30.0.2"
        minSdkVersion = 24
        compileSdkVersion = 30
        targetSdkVersion = 30
        playServicesVersion = "18.0.0"
        androidMapsUtilsVersion = "2.2.0"
        googlePlayServicesLocationVersion = "18.0.0"    // Or higher.
        appCompatVersion = "1.1.0"
    }
    repositories {
        google()
        maven {
            // Required for react-native-background-geolocation
            url("${project(':react-native-background-geolocation').projectDir}/libs")
        }
        mavenCentral {
            // We don't want to fetch react-native from Maven Central as there are
            // older versions over there.
            content {
                excludeGroup "com.facebook.react"
            }
        }
        maven {
            // Required for react-native-background-fetch
            url("${project(':react-native-background-fetch').projectDir}/libs")
        }
    }
    dependencies {
        classpath('com.android.tools.build:gradle:4.2.2')
        classpath("com.bugsnag:bugsnag-android-gradle-plugin:5.+")
        // NOTE: Do not place your application dependencies here; they belong
        // in the individual module build.gradle files
        
        // Required by react-native-push-notification
        classpath('com.google.gms:google-services:4.3.8')
    }
}

allprojects {
    repositories {
        mavenCentral()
        mavenLocal()
        maven {
            // All of React Native (JS, Obj-C sources, Android binaries) is installed from npm
            url("$rootDir/../node_modules/react-native/android")
        }
        maven {
            // Android JSC is installed from npm
            url("$rootDir/../node_modules/jsc-android/dist")
        }

        google()
        jcenter()
        maven { url 'https://www.jitpack.io' }

        maven {
            // Required for react-native-background-geolocation
            url("${project(':react-native-background-geolocation').projectDir}/libs")
        }
        maven {
            // Required for react-native-background-fetch
            url("${project(':react-native-background-fetch').projectDir}/libs")
        }

        // Required by /react-native-image-crop-picker
        maven { url 'https://maven.google.com' }
        // Required by /react-native-image-crop-picker
        maven { url "https://www.jitpack.io" }

        maven {
            url 'https://pkgs.dev.azure.com/bconline/ASG/_packaging/MobileAppDevelopment/maven/v1'
            name 'MobileAppDevelopment'
            credentials {
                username vstsUsername
                password vstsMavenAccessToken
            }
        }
    }
}
